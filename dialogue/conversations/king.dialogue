~ start
if QuestManager.ready_for_turnin_quests.has("kill_bandits")
	=> turnin_kill_bandits
elif QuestManager.ready_for_turnin_quests.has("gather_lilies")
	=> turnin_gather_lilies
else
	King: Welcome to the lands of Brynd!
	=> talk_to_king

~ talk_to_king
- What is there to do there?
	King: Shops, quests, and more! Take a look around!
	=> talk_to_king
- Can you tell me about controls?
	King: Sure! Your movement is your basic WASD/Arrow Keys or Joysticks.
	King: To interact with objects you press E, or if its in an Interface, click on it.
	King: For controller, that is A or X to click in an interface.
	King: For combat, use your left click and right click, left is a quick attack.
	King: While right click is a longer Area of Effect attack that can hit multiple nearby enemies.
	King: This is replaced by the Left and Right triggers for controller.
	King: To dash, use Shift or Y, and to heal use Middle Mouse Button or B
	King: To open the Quest Panel hit Q or Right on the D-Pad.
	King: To open your Inventory / Equipment Pane, hit tab or left on the D-Pad.
	King: To open the main menu hit ~ or P or the menu button on your controller.
	King: There is no saving in this demo currently so the menu button is just for exit game.
	King: Have fun!
	=> talk_to_king
- Do you have any quests? 
	King: Indeed I do. The kingdom always needs brave adventurers.
	- Bandits! [if QuestManager.can_repeat_quest("kill_bandits")] 
		King: The bandits in the Western Forest have been attacking travelers.
		King: Kill 5 bandits and you'll be rewarded with {{QuestManager.quests["kill_bandits"].rewards.gold}} gold!
		- I'll take this quest! 
			King: Excellent! The Western Forest is just beyond the village. Good hunting!
			do QuestManager.start_quest("kill_bandits")
			=> END
		- I'll think about it
			=> END
	- Message Delivery [if QuestManager.can_repeat_quest("deliver_message")]
		King: I have a message for the Vendor.
		King: Deliver it for a reward of {{QuestManager.quests["deliver_message"].rewards.gold}} gold!
		- I'll deliver your message
			King: Thank you! The Vendor is by his house in this village.
			do QuestManager.start_quest("deliver_message")
			=> END
		- Not interested
			=> END
	- Find Farmer Fred [if QuestManager.can_repeat_quest("find_fred")]
		King: I need someone to deliver an important message to Farmer Fred.
		King: His farm is located outside the village, through a cave in the hills.
		King: He'll reward you for delivering this letter to him.
		- I'll find Farmer Fred
			King: Thank you! Be careful on your journey, the path can be dangerous.
			do QuestManager.start_quest("find_fred")
			=> END
		- Not right now
			=> END
	- Healing Lilies [if QuestManager.can_repeat_quest("gather_lilies")]
		King: Our royal healer is running low on medicinal supplies.
		King: She needs 5 healing lilies to prepare medicine for the sick.
		King: These lilies grow in the meadows around the kingdom. Can you gather them?
		- I'll gather the lilies
			King: Thank you! The lilies have a distinctive appearance.
			King: Tall green stems with white flowers on top.
			King: Bring them back to me when you've collected all 5.
			do QuestManager.start_quest("gather_lilies")
			=> END
		- Not right now
			=> END
	- Never mind
		=> END
- End the conversation 
	=> END
King: More dialogue coming soon!
=> END

~ turnin_kill_bandits
King: Thank you! Here is your reward as promised.
Reward: {{QuestManager.quests["kill_bandits"].rewards.experience}} exp | {{QuestManager.quests["kill_bandits"].rewards.gold}} coins.
do QuestManager.complete_quest("kill_bandits")
=> start

~ turnin_gather_lilies
King: Ah, you've collected all the healing lilies! The royal healer will be most pleased.
King: These will help treat many of our sick citizens. Here's your reward.
Reward: {{QuestManager.quests["gather_lilies"].rewards.experience}} exp | {{QuestManager.quests["gather_lilies"].rewards.gold}} coins.
do QuestManager.complete_quest("gather_lilies")
=> start